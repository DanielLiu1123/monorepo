plugins {
    id "org.springframework.boot" version "${springBootVersion}" apply false
    id "io.spring.dependency-management" version "${dependencyManagementPluginVersion}" apply false
    id "com.diffplug.spotless" version "${spotlessPluginVersion}" apply false
    id "com.google.protobuf" version "${protobufPluginVersion}" apply false
    id "net.ltgt.errorprone" version "${errorPronePluginVersion}" apply false
}

//allprojects { // uncomment this if you want to get IDE support, only for debugging purpose!
subprojects {
    apply plugin: "java"
    apply plugin: "java-library"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "com.diffplug.spotless"

    java {
        sourceCompatibility = 25
        targetCompatibility = 25
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += ["-parameters"]
        options.encoding = "UTF-8"
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "io.github.danielliu1123:grpc-starter-dependencies:${grpcStarterVersion}"
            mavenBom "io.grpc:grpc-bom:${grpcVersion}"
            mavenBom "com.google.protobuf:protobuf-bom:${protobufVersion}"
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
    }

    dependencies {
        compileOnly("org.projectlombok:lombok")
        annotationProcessor("org.projectlombok:lombok")
        testCompileOnly("org.projectlombok:lombok")
        testAnnotationProcessor("org.projectlombok:lombok")

        testImplementation("org.junit.jupiter:junit-jupiter")
        testRuntimeOnly("org.junit.platform:junit-platform-launcher")
        testImplementation("org.assertj:assertj-core")
    }

    test {
        useJUnitPlatform()
        jvmArgs("-XX:+EnableDynamicAgentLoading")
    }

    spotless {
        encoding "UTF-8"
        java {
            toggleOffOn()
            removeUnusedImports()
            trimTrailingWhitespace()
            endWithNewline()
            palantirJavaFormat()

            targetExclude(
                    "build/generated/**",
                    "**/entity/*.java", // MyBatis generated entities
                    "**/mapper/*Mapper.java", // MyBatis generated mappers
                    "**/mapper/*DynamicSqlSupport.java" // mybatis-dynamic-sql generated classes
            )

            custom("Refuse wildcard imports", {
                if (it =~ /\nimport .*\*;/) {
                    throw new IllegalStateException("Do not use wildcard imports, 'spotlessApply' cannot resolve this issue, please fix it manually.")
                }
            } as Closure<String>)
        }
    }

    apply plugin: "net.ltgt.errorprone"
    dependencies {
        errorprone "com.google.errorprone:error_prone_core:${errorProneCoreVersion}"
        errorprone "com.uber.nullaway:nullaway:${nullAwayVersion}"
    }

    tasks.withType(JavaCompile).configureEach {
        options.errorprone {
            // https://github.com/tbroyer/gradle-errorprone-plugin?tab=readme-ov-file#properties
            excludedPaths = ".*/(generated|proto-gen-java|entity|mapper)/.*"
            // https://github.com/uber/NullAway/wiki/Configuration
            check("NullAway", net.ltgt.gradle.errorprone.CheckSeverity.ERROR)
            option("NullAway:AnnotatedPackages", "grpcstarter")
            option("NullAway:HandleTestAssertionLibraries", "true")
        }
    }
}
