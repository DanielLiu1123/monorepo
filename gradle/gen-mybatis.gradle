// Usage:
// 1. Add 'apply from: "${rootDir}/gradle/gen-mybatis.gradle"' in the module's build.gradle
// 2. Add a 'generatorConfig.xml' in the module's root directory
// 3. Run `./gradlew genMyBatis --project-dir <module-path>`, e.g. `./gradlew genMyBatis --project-dir services/todo-service`

configurations {
    generator
}

dependencies {
    generator "org.mybatis.generator:mybatis-generator-core:${mybatisGeneratorVersion}"
    generator "org.postgresql:postgresql"
    generator project(":packages:lib-java:mybatis")
}

tasks.register("genMyBatis", JavaExec) {
    group = "codegen"
    description = "Generates MyBatis artifacts"

    classpath = configurations.generator
    mainClass = "org.mybatis.generator.api.ShellRunner"
    args = ["-configfile", "${projectDir}/generatorConfig.xml", "-overwrite", "-verbose"]
}
